"deb_import"

load(":deb_archive.bzl", "data_archive", "host_bsdtar")
load(":linker_script.bzl", "linker_script")
load(":lockfile.bzl", "lockfile")
load(":pkgconfig.bzl", "pkgconfig")
load(":util.bzl", "util")

# BUILD.bazel template
_DEB_IMPORT_BUILD_TMPL = '''
load("@rules_distroless//apt/private:deb_postfix.bzl", "deb_postfix")
load("@rules_distroless//apt/private:deb_export.bzl", "deb_export")
load("@rules_distroless//apt/private:so_library.bzl", "so_library")
load("@rules_cc//cc/private/rules_impl:cc_import.bzl", "cc_import")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_distroless//apt/private:cc_deb_library.bzl", "cc_deb_library")
load("@bazel_skylib//rules/directory:directory.bzl", "directory")

deb_postfix(
    name = "data",
    srcs = glob(["data.tar*"]),
    outs = ["content.tar.gz"],
    mergedusr = {mergedusr},
    visibility = ["//visibility:public"],
)

filegroup(
    name = "control",
    srcs = glob(["control.tar.*"]),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "{target_name}",
    srcs = {depends_on} + [":data"],
    visibility = ["//visibility:public"],
)


deb_export(
    name = "export",
    srcs = glob(["data.tar*"]),
    foreign_symlinks = {foreign_symlinks},
    symlink_outs = {symlink_outs},
    outs = {outs},
    visibility = ["//visibility:public"]
)

directory(
    name = "directory",
    srcs = {symlink_outs} + {outs},
    visibility = ["//visibility:public"]
)

{cc_import_targets}
'''

_CC_LIBRARY_LIBC_TMPL = """
alias(
    name = "{name}_wodeps",
    actual = ":{name}",
    visibility = ["//visibility:public"]
)

cc_library(
    name = "{name}",
    hdrs = {hdrs},
    additional_compiler_inputs = {additional_compiler_inputs},
    additional_linker_inputs = {additional_linker_inputs},
    includes = {includes},
    linkopts = {linkopts},
    visibility = ["//visibility:public"],
)
"""

_CC_IMPORT_TMPL = """
cc_import(
    name = "{name}",
    hdrs = {hdrs},
    includes = {includes},
    linkopts = {linkopts},
    shared_library = {shared_lib},
    static_library = {static_lib},
)
"""

_CC_LIBRARY_TMPL = """
cc_library(
    name = "{name}_wodeps",
    hdrs = {hdrs},
    deps = {direct_deps},
    linkopts = {linkopts},
    additional_compiler_inputs = {additional_compiler_inputs},
    additional_linker_inputs = {additional_linker_inputs},
    strip_include_prefix = {strip_include_prefix},
    visibility = ["//visibility:public"],
)

cc_library(
    name = "{name}",
    deps = [":{name}_wodeps"] + {deps},
    visibility = ["//visibility:public"],
)
"""

_CC_SYS_LIBRARY_TMPL = """
cc_deb_library(
    name = "{name}_wodeps",
    hdrs = {hdrs},
    deps = {direct_deps},
    linkopts = {linkopts},
    additional_compiler_inputs = {additional_compiler_inputs},
    additional_linker_inputs = {additional_linker_inputs},
    visibility = ["//visibility:public"],
)

cc_library(
    name = "{name}",
    deps = [":{name}_wodeps"] + {deps},
    visibility = ["//visibility:public"],
)
"""

def resolve_symlink(target_path, relative_symlink):
    # Split paths into components
    target_parts = target_path.split("/")
    symlink_parts = relative_symlink.split("/")

    # Remove the file name from target path to get the directory
    target_dir_parts = target_parts[:-1]

    # Process the relative symlink
    result_parts = target_dir_parts[:]
    for part in symlink_parts:
        if part == "..":
            # Move up one directory by removing the last component
            if result_parts:
                result_parts.pop()
        elif part == "." or part == "":
            # Ignore current directory or empty components
            continue
        else:
            # Append the component to the path
            result_parts.append(part)

    # Join the parts back into a path
    resolved_path = "/".join(result_parts)
    return resolved_path

# Scratch directory (inside the repo) into which we extract the files we need to
# inspect on disk.
_EXTRACT_DIR = "_extracted"

def _remap_linkopts(rctx, extract_dir, so_regular_files, self_files, depends_file_map, target_name):
    # Some .so files are GNU ld linker scripts (e.g. libc.so, libbsd.so) that
    # reference libraries by absolute install path (e.g. /usr/lib/x86_64-linux-gnu/libbsd.so.0.11.7).
    # The hermetic linker cannot open those paths,
    # so we emit --remap-inputs pointing at the Bazel-provided copy of each referenced file (in this package or a dependency).
    # The candidate .so files are expected to have already been extracted into `extract_dir`.
    if not so_regular_files:
        return []

    # `grep -I` skips binary (ELF) files, leaving only the text linker scripts.
    scratch_paths = [extract_dir + "/" + so for so in so_regular_files]
    grep = rctx.execute(
        ["grep", "-I", "-l", "-e", "OUTPUT_FORMAT", "-e", "GROUP", "-e", "INPUT"] + scratch_paths,
    )

    referenced = []
    for script_path in grep.stdout.splitlines():
        referenced += linker_script.files_to_remap(rctx.read(script_path))

    result = linker_script.remap_linkopts(referenced, self_files, depends_file_map, rctx.attr.name)

    if len(result.unresolved):
        util.warning(
            rctx,
            "some linker-script paths could not be resolved for {}. unresolved: {}".format(
                target_name,
                json.encode_indent(result.unresolved),
            ),
        )

    return result.linkopts

def _discover_contents(rctx, depends_on, depends_file_map, target_name, mergedusr = False, repo_prefix = ""):
    archive = data_archive(rctx)
    tar = host_bsdtar(rctx)
    result = rctx.execute([tar, "-tvf", archive])
    if result.return_code:
        fail("failed to inspect %s: %s" % (archive, result.stderr))
    contents_raw = result.stdout.splitlines()

    so_files = []
    a_files = []
    h_files = []
    hpp_files = []
    hpp_files_woext = []
    pc_files = []
    o_files = []
    symlinks = {}
    so_regular_files = []

    for line in contents_raw:
        # Skip directories
        if line.endswith("/"):
            continue

        line = line[line.find(" ./") + 3:]

        # Skip everything in man pages and examples
        if line.startswith("usr/share"):
            continue

        is_symlink_idx = line.find(" -> ")
        resolved_symlink = None
        if is_symlink_idx != -1:
            symlink_target = line[is_symlink_idx + 4:]
            line = line[:is_symlink_idx]
            if line.endswith(".pc"):
                continue

            # An absolute symlink
            if symlink_target.startswith("/"):
                resolved_symlink = symlink_target.removeprefix("/")
            else:
                resolved_symlink = resolve_symlink(line, symlink_target).removeprefix("./")

        if (line.endswith(".so") or line.find(".so.") != -1) and line.find("lib") != -1:
            if line.find("libthread_db") != -1:
                continue
            so_files.append(line)

            # Regular-file .so (not a symlink) may be a GNU ld linker script.
            if resolved_symlink == None:
                so_regular_files.append(line)
        elif line.endswith(".a") and line.find("lib"):
            a_files.append(line)
        elif line.endswith(".pc") and line.find("pkgconfig"):
            pc_files.append(line)
        elif line.endswith(".h"):
            h_files.append(line)
        elif line.endswith(".hpp"):
            hpp_files.append(line)
        elif line.find("include/c++") != -1 or (line.find("usr/include/") != -1 and line[line.rfind("/") + 1:].find(".") == -1):
            hpp_files_woext.append(line)
        elif line.endswith(".o"):
            o_files.append(line)
        else:
            continue

        if resolved_symlink:
            symlinks[line] = resolved_symlink

    # Resolve symlinks
    # For each symlink, we check:
    #  - If provided by a dependency -> rewrite to that dependency's label
    #  - If shipped by this package  -> drop it from `symlinks` so it is emitted as a plain output
    #  - Else                        -> leave unresolved, and warn
    self_files = {
        f: None
        for f in so_files + h_files + hpp_files + a_files + hpp_files_woext
    }
    unresolved_symlinks = {}
    for (symlink, symlink_target) in symlinks.items():
        dep_repo = depends_file_map.get(symlink_target)
        if dep_repo:
            # dep_repo is a canonical repo name, so reference it with "@@".
            symlinks[symlink] = "@@{dep}//:{file}".format(dep = dep_repo, file = symlink_target)
        elif symlink_target in self_files:
            symlinks.pop(symlink)
        else:
            unresolved_symlinks[symlink] = symlink_target

    if len(unresolved_symlinks):
        util.warning(
            rctx,
            "some symlinks could not be solved for {}. \nresolved: {}\nunresolved:{}".format(
                target_name,
                json.encode_indent(symlinks),
                json.encode_indent(unresolved_symlinks),
            ),
        )

    # Extract, in a single pass, the files we need to inspect on disk:
    # - `.so` regular files because they may be linker scripts we need to inspect,
    # - pkgconfig files because we need them to generate cc targets.
    files_to_extract = so_regular_files + pc_files
    if files_to_extract:
        rctx.execute(["mkdir", "-p", _EXTRACT_DIR])
        result = rctx.execute(
            [tar, "-xf", archive, "-C", _EXTRACT_DIR] +
            ["./" + f for f in files_to_extract],
        )
        if result.return_code:
            fail("failed to extract %s from %s: %s" % (files_to_extract, archive, result.stderr))

    remap_linkopts = _remap_linkopts(
        rctx,
        _EXTRACT_DIR,
        so_regular_files,
        self_files,
        depends_file_map,
        target_name,
    )

    outs = []

    for out in so_files + h_files + hpp_files + a_files + hpp_files_woext + o_files:
        if out not in symlinks:
            outs.append(out)

    deps = []
    for dep in depends_on:
        (suite, name, arch, version) = lockfile.parse_package_key(dep)
        deps.append(
            "@%s//:%s_wodeps" % (repo_prefix + util.package_repo_name(dep, mergedusr = mergedusr), name.removesuffix("-dev")),
        )

    pkgconfigs = []
    for pc in pc_files:
        if rctx.path(_EXTRACT_DIR + "/" + pc).exists:
            pkgconfigs.append(pc)

    build_file_content = """
so_library(
    name = "_so_libs",
    dynamic_libs = {}
)
""".format(so_files)

    rpaths = {}
    for so in so_files + a_files:
        rpath = so[:so.rfind("/")]
        rpaths[rpath] = None

    # Package has a pkgconfig, use that as the source of truth.
    if len(pkgconfigs):
        link_paths = []
        includes = []

        static_lib = None
        shared_lib = None

        import_targets = []

        for pc_file in pkgconfigs:
            pkgc = pkgconfig(rctx, _EXTRACT_DIR + "/" + pc_file)
            includes += pkgc.includes
            link_paths += pkgc.link_paths

            if len(pkgc.libnames) == 0:
                continue

            for libname in pkgc.libnames:
                if libname + "_import" in import_targets:
                    continue

                subtarget = libname + "_import"
                import_targets.append(subtarget)

                # Look for a static archive
                # for ar in a_files:
                #     if ar.endswith(pkgc.libname + ".a"):
                #         static_lib = '":%s"' % ar
                #         break

                # Look for a dynamic library
                IGNORE = ["libfl"]
                for so_lib in so_files:
                    if libname and libname not in IGNORE and so_lib.endswith(libname + ".so"):
                        shared_lib = '":%s"' % so_lib
                        break

                build_file_content += _CC_IMPORT_TMPL.format(
                    name = subtarget,
                    shared_lib = shared_lib,
                    static_lib = static_lib,
                    hdrs = [],
                    includes = {
                        "external/.." + include: True
                        for include in includes + ["/usr/include", "/usr/include/x86_64-linux-gnu"]
                    }.keys(),
                    linkopts = pkgc.linkopts,
                )

        build_file_content += _CC_LIBRARY_TMPL.format(
            name = target_name,
            hdrs = h_files + hpp_files,
            additional_compiler_inputs = hpp_files_woext,
            additional_linker_inputs = so_files + o_files,
            linkopts = {
                opt: True
                for opt in [
                    # # Needed for cc_test binaries to locate its dependencies.
                    # "-Wl,-rpath=../{}/{}".format(rctx.attr.name, rpath)
                    # for rp in rpaths
                ] + [
                    # Needed for cc_test binaries to locate its dependencies as a build tool
                    # "-Wl,-rpath=./external/{}/{}".format(rctx.attr.name, rpath)
                    # for rp in rpaths
                ] + [
                    "-L$(BINDIR)/external/{}/{}".format(rctx.attr.name, lp)
                    for lp in link_paths
                ] + [
                    "-Wl,-rpath=/" + rp
                    for rp in rpaths
                ] + remap_linkopts
            }.keys(),
            direct_deps = import_targets + [":_so_libs"],
            deps = deps,
            strip_include_prefix = None,
        )

    elif (len(hpp_files) or len(h_files)) and ((target_name.find("libc") != -1 or target_name.find("libstdc") != -1 or target_name.find("libgcc") != -1)):
        build_file_content += _CC_LIBRARY_LIBC_TMPL.format(
            name = target_name,
            hdrs = h_files + hpp_files,
            additional_compiler_inputs = hpp_files_woext,
            additional_linker_inputs = so_files + a_files + o_files,
            includes = [],
            linkopts = remap_linkopts,
        )
    else:
        build_file_content += _CC_SYS_LIBRARY_TMPL.format(
            name = target_name,
            hdrs = h_files + hpp_files,
            deps = deps,
            additional_compiler_inputs = hpp_files_woext,
            additional_linker_inputs = so_files + o_files,
            linkopts = [
                # Required for linker to find .so libraries
                "-L$(BINDIR)/external/{}/{}".format(rctx.attr.name, rp)
                for rp in rpaths
            ] + [
                # # Required for bazel test binary to find its dependencies.
                # "-Wl,-rpath=../{}/{}".format(rctx.attr.name, rp)
                # for rp in rpaths
            ] + [
                # Required for ld to validate rpath entries
                "-Wl,-rpath-link=$(BINDIR)/external/{}/{}".format(rctx.attr.name, rp)
                for rp in rpaths
            ] + [
                # Required for containers to find the dependencies at runtime.
                "-Wl,-rpath=/" + rp
                for rp in rpaths
            ] + remap_linkopts,
            direct_deps = [":_so_libs"],
        )

    return (build_file_content, outs, symlinks)

def _deb_import_impl(rctx):
    rctx.download_and_extract(
        url = rctx.attr.urls,
        sha256 = rctx.attr.sha256,
    )

    # Rebuild the {file: canonical_name(dependency_repo)} index from each dependency's own filemap.
    # The first dependency that provides a path wins.
    provided_by = {}
    for i in range(len(rctx.attr.dep_filemaps)):
        filemap_path = rctx.path(rctx.attr.dep_filemaps[i])
        dep_repo = filemap_path.dirname.basename.removesuffix("_filemap")
        for file in json.decode(rctx.read(filemap_path)):
            if file not in provided_by:
                provided_by[file] = dep_repo

    # TODO: only do this if package is -dev or dependent of a -dev pkg.
    cc_import_targets, outs, symlinks = _discover_contents(
        rctx,
        rctx.attr.depends_on,
        provided_by,
        rctx.attr.package_name.removesuffix("-dev"),
        mergedusr = rctx.attr.mergedusr,
        repo_prefix = rctx.attr.repo_prefix,
    )

    foreign_symlinks = {}
    for (i, symlink) in enumerate(symlinks.values()):
        if symlink not in foreign_symlinks:
            foreign_symlinks[symlink] = []
        foreign_symlinks[symlink].append(i)

    foreign_symlinks = {
        symlink: json.encode(indices)
        for (symlink, indices) in foreign_symlinks.items()
    }

    rctx.file("BUILD.bazel", _DEB_IMPORT_BUILD_TMPL.format(
        mergedusr = rctx.attr.mergedusr,
        depends_on = ["@" + rctx.attr.repo_prefix + util.package_repo_name(dep_key, mergedusr = rctx.attr.mergedusr) + "//:data" for dep_key in rctx.attr.depends_on],
        target_name = rctx.attr.target_name,
        cc_import_targets = cc_import_targets,
        outs = outs,
        foreign_symlinks = foreign_symlinks,
        symlink_outs = symlinks.keys(),
    ))

deb_import = repository_rule(
    implementation = _deb_import_impl,
    attrs = {
        "urls": attr.string_list(mandatory = True, allow_empty = False),
        "sha256": attr.string(),
        "depends_on": attr.string_list(doc = "Names of packages this package depends on"),
        "dep_filemaps": attr.label_list(doc = "Each dependency's filemap.json, in depends_on order, used to resolve cross-package symlinks."),
        "mergedusr": attr.bool(),
        "target_name": attr.string(),
        "package_name": attr.string(),
        "repo_prefix": attr.string(doc = "Internal namespace for independent checked locks."),
    },
)
